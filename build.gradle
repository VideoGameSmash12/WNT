plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
}

group 'me.videogamesm12'
version '2.0-alpha.1'

repositories {
}

dependencies {
    // Text components
    implementation include("net.kyori:adventure-text-serializer-gson:4.9.3")
    include("net.kyori:adventure-api:4.12.0")
    include("net.kyori:adventure-key:4.12.0")
    include("net.kyori:examination-api:1.3.0")

    subprojects.each {
        include project("${it.name}:")
    }
}

allprojects {
    apply plugin: 'fabric-loom'

    tasks.withType(JavaCompile).configureEach {
        it.options.release.set(16)
    }

    repositories {
        maven {
            url "https://maven.shedaniel.me/"
        }
        maven {
            url "https://maven.terraformersmc.com/releases/"
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:1.19.1"
        mappings "net.fabricmc:yarn:1.19.1+build.6:v2"
        modImplementation "net.fabricmc.fabric-api:fabric-api:0.58.5+1.19.1"

        modImplementation "net.fabricmc:fabric-loader:0.12.12"
        modApi("me.shedaniel.cloth:cloth-config-fabric:5.3.58") {
            exclude(group: "net.fabricmc.fabric-api")
        }
        modImplementation "com.terraformersmc:modmenu:2.0.14"
        implementation "net.kyori:adventure-api:4.9.3"

        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }

    processResources {
        inputs.property "version", rootProject.version
        filteringCharset "UTF-8"

        filesMatching("fabric.mod.json") {
            expand "version": rootProject.version
        }
    }
}

subprojects {
    group rootProject.group
    version rootProject.version

    dependencies {
        implementation rootProject
    }
}

subprojects.each {
    remapJar.dependsOn("${it.path}:remapJar")
}