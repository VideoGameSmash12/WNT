plugins {
        id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'maven-publish'
}

group 'me.videogamesm12'
version '2.0-alpha.13'

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    // Text components
    implementation include("net.kyori:adventure-text-serializer-gson:4.13.1")
    include("net.kyori:adventure-api:4.13.1")
    include("net.kyori:adventure-key:4.13.1")
    include("net.kyori:examination-api:1.3.0")

    // CFX has since been moved to a separate project
    include("maven.modrinth:cfx:YRSLKiNN")

    subprojects.each {
        include project("${it.name}:")
    }
}

allprojects {
    apply plugin: 'fabric-loom'

    tasks.withType(JavaCompile).configureEach {
        it.options.release.set(16)
    }

    repositories {
        maven {
            url "https://maven.shedaniel.me/"
        }
        maven {
            url "https://maven.terraformersmc.com/releases/"
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:1.19.3"
        mappings "net.fabricmc:yarn:1.19.3+build.5:v2"
        modImplementation "net.fabricmc.fabric-api:fabric-api:0.75.1+1.19.3"

        modImplementation "net.fabricmc:fabric-loader:0.14.17"
        modImplementation "com.terraformersmc:modmenu:5.0.2"
        implementation "net.kyori:adventure-api:4.13.1"

        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
    }

    processResources {
        inputs.property "version", rootProject.version
        filteringCharset "UTF-8"

        filesMatching("fabric.mod.json") {
            expand "version": rootProject.version
        }
    }
}

subprojects {
    group rootProject.group
    version rootProject.version

    dependencies {
        implementation rootProject
    }
}

subprojects.each {
    remapJar.dependsOn("${it.path}:remapJar")
}
